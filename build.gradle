apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        throw new GradleException('VersionName could not be read:' + ignored.message)
    }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return Integer.valueOf(stdout.toString().trim())
    } catch (ignored) {
        throw new GradleException('VersionCode could not be read:' + ignored.message)
    }
}

android {
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    compileSdk 35

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    androidResources {
        generateLocaleConfig = true
    }

    defaultConfig {
        applicationId 'de.dennisguse.opentracks'
        versionCode 5980
        versionName "v4.17.2"

        buildConfigField "String", "VERSION_NAME_FULL", "\"${getVersionName()}\""

        minSdk 26
        targetSdk 35

        testInstrumentationRunner 'de.dennisguse.opentracks.TestRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    signingConfigs {
        nightly {
            if (System.getProperty('nightly_store_file') != null) {
                storeFile file(System.getProperty('nightly_store_file'))
                storePassword System.getProperty('nightly_store_password')
                keyAlias System.getProperty('nightly_key_alias')
                keyPassword System.getProperty('nightly_key_password')
            }
        }
    }

    namespace 'de.dennisguse.opentracks'

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            crunchPngs false
            minifyEnabled false
        }
    }


    flavorDimensions 'version'
    productFlavors {
        nightly {
            dimension 'version'
            applicationIdSuffix '.nightly'
            signingConfig signingConfigs.nightly
        }
        irreproducible {
            // Non-reproducible: https://f-droid.org/de/packages/de.dennisguse.opentracks/
            dimension 'version'
            applicationId 'de.dennisguse.opentracks'
            versionNameSuffix 'irreproducible'
        }
         reproducible {
            // Developer Binaries: https://github.com/OpenTracksApp/OSMDashboard/releases/download/v%v/de.dennisguse.opentracks.playstore_%v.apk
            // FDroid: https://f-droid.org/de/packages/de.dennisguse.opentracks.playstore
            // PlayStore: https://play.google.com/store/apps/details?id=de.dennisguse.opentracks.playstore
            dimension 'version'
            applicationId 'de.dennisguse.opentracks.playstore'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.resValue 'string', 'applicationId', variant.applicationId

        variant.outputs.configureEach {
            if (variant.buildType.name == 'reproducible') {
                outputFileName = "${applicationId}_${variant.buildType.name}_${variant.versionName}.apk"
            } else {
                outputFileName = "${applicationId}_${variant.buildType.name}_${variant.versionCode}.apk"
            }

            if (variant.buildType.name == 'nightly') {
                setVersionCodeOverride(getVersionCode())
                setVersionNameOverride(getVersionName())
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.mediarouter:mediarouter:1.7.0'
    implementation 'androidx.core:core-location-altitude:1.0.0-alpha03'

    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'org.mockito:mockito-android:5.14.2'

    androidTestUtil 'androidx.test:orchestrator:1.5.1'
}
