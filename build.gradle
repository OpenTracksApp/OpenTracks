apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        exec {
            commandLine 'git', 'log', '--format="%h"', '-n 1'
            standardOutput = stdout
        }
        return stdout.toString().replace('\n', '-').replace('"', '').trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId "de.dennisguse.opentracks"
        manifestPlaceholders = [
                applicationLogo: "@mipmap/ic_launcher",
                applicationName: "@string/app_name"
        ]

        versionCode 3466
        versionName "v3.8.0"

        buildConfigField "String", "VERSION_NAME_FULL", "\"${getVersionName()}\""



        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [
                    applicationLogo: "@mipmap/ic_launcher_debug",
                    applicationName: "OpenTracks (Debug)"
            ]
        }

        release {
            minifyEnabled false
        }

        releasePlayStore {
            applicationIdSuffix ".playstore"
            versionNameSuffix "-PlayStore"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "applicationId", variant.applicationId
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'org.mockito:mockito-android:3.4.4'
}
